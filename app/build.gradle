apply plugin: 'com.android.application'
//apply plugin: 'android-apt'
android {
    signingConfigs {
        release {
            //加载资源
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)

            //读取文件
            def sdkDir = properties.getProperty('keystore.path')
            storeFile file(sdkDir)

            //读取字段
            def key_keyAlias = properties.getProperty('keystore.alias')
            def key_keyPassword = properties.getProperty('keystore.password')
            def key_storePassword = properties.getProperty('keystore.alias_password')

            keyAlias key_keyAlias
            keyPassword key_keyPassword
            storePassword key_storePassword
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.lxl.nanosic.app"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 7
        versionName "1.0.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // 设置Cmake参数
        externalNativeBuild {
            cmake {
                cppFlags "" //编译参数
                abiFilters 'armeabi-v7a'//,'arm64-v8a','x86','x86_64' //支持版本
            }
        }
    }
    
    // 配置CMakeLists.txt路径
    /*
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }*/

    buildTypes {

        release {
            zipAlignEnabled true //4字节对齐，减少运行内存消耗　
            minifyEnabled true   //false = 关闭混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            zipAlignEnabled true //4字节对齐，减少运行内存消耗　
            minifyEnabled false  //false = 关闭混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ["src/main/jni/jniLibs"]
        }
    }

    packagingOptions { //For Error: More than one file was found with OS independent path
        pickFirst 'lib/armeabi-v7a/libappJNI.so'
    }

    android {
        useLibrary 'org.apache.http.legacy'
    }

    //自定义apk名称含版本号信息
    android.applicationVariants.all { variant ->
        // 更新至Android Studio 3.3 gradle 4.10.1
        variant.outputs.all {
            outputFileName = "Nanosic_OTA_${variant.versionCode}-${variant.versionName}-${releaseTime()}.apk"
        }

        //更新前
        /*
        variant.outputs.all { output ->
            def fileName = "Nanosic_OTA_${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
            //def fileName = outputFile.name.replace("app", "${rootProject.ext.appName}-${releaseTime()}-${defaultConfig.versionCode}-${defaultConfig.versionName}")
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                outputFileName = fileName
            }
        }
        */
    }
}

//获取编译时间
static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //annotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okio:okio:1.14.0'
    implementation "com.google.code.gson:gson:2.8.2"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
